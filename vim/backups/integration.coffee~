describe 'Integration of the Entire Application', ->

  # fixtures 

  app = {}
  $location = {}
  $rootScope = {}
  Session = {}
  sessionData = {}

  # modules

  beforeEach module 'depositionsApp'
  beforeEach module 'views/depositionNavbar.html'
  beforeEach module 'views/main.html'
  beforeEach module 'views/depos.html'
  beforeEach module 'views/exhibits.html'

  # setup integrated fixtures

  beforeEach inject ($compile, _$rootScope_)->
    $rootScope = _$rootScope_
    html = '''
      <div ng-app="depositionsApp">
        <div deposition-navbar />
        <div ng-view></div>
      </div>
    '''
    app = ($compile html)($rootScope.$new())

  beforeEach inject (_$location_, _Session_, _sessionData_)->
    $location = _$location_
    Session = _Session_
    sessionData = _sessionData_

  # helpers
  
  digestAndExpectLocationToBe = (path)->
      $rootScope.$digest()
      (expect $location.path()).toBe path

  testRedirect = (path, destination)->
    $location.url path
    digestAndExpectLocationToBe destination
  
  it 'when authenticated, it should redirect as requested',->
    Session.login(sessionData.password)
    digestAndExpectLocationToBe '/'
    testRedirect '/depos',    '/depos'
    testRedirect '/exhibits', '/exhibits'
    testRedirect '/',         '/'

  it 'unless authenticated, it should redirect to root', ->
    Session.logout()
    digestAndExpectLocationToBe '/'
    testRedirect '/depos',    '/'
    testRedirect '/exhibits', '/'
    testRedirect '/',         '/'
